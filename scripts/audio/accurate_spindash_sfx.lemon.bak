/*

	This script file is part of the Sonic 3 A.I.R. Modders Resource. The Modders
	Resource is a free resource library for any 3 A.I.R. modder, and for any
	non-commercial purpose. No permission to use needed, no crediting required!

	Come check it out at https://github.com/AirWay1/3AIR-Mod-Resources!

	Script file created and donated for usage by AirWay1.

*/

// Author Notes
/*

	Change Spindash.maxLimit to the value of your desired spindash length.

	So for example, if you want to play up to 6 sounds, you would change
	Spindash.maxlimit to 6.

	Sounds are defined as "spindash_0xY". Changing Y to a hexadecimal number.
	The sounds begin at "spindash_0x1" and end at "spindash_0x[Spindash.maxLimit]".

*/

// Read Author Notes for more information. Default is 0xE (15 in decimal value).
global u8 Spindash.maxLimit = 0xE

// Counter for spindash sound (required by script, would not recommend touching).
global u8 Spindash.counter = 0

function u64 Standalone.getModdedSoundKey(u64 soundKey, u8 sfxId, u8 soundRegType)
{
	// Is the sfxId the spindash charging sound?
	if (sfxId == SFX_SPINDASH_CHARGE)
	{
		// Increment the counter if the limit was not reached.
		if (Spindash.counter != Spindash.maxLimit + 1)
			Spindash.counter++

		// Return our new soundKey.
		return soundKey = Audio.spindashSoundKey()
	}

	// Always return a base value if the function is not a void.
	return base.Standalone.getModdedSoundKey(soundKey, sfxId, soundRegType)
}

function u64 Audio.spindashSoundKey()
{
	u64 soundKeyCheck = stringformat("spindash_0x%x", Spindash.counter - 1)

	if (Spindash.counter != Spindash.maxLimit + 1)
	{
		//Check if the audio is playing, if not, reset the counter. This is how S3&K handles it by the way.
		if (!Audio.isPlayingAudio(soundKeyCheck))
			Spindash.counter = 0
	}
	else
	{
		if (!Audio.isPlayingAudio(soundKeyCheck))
		{
			Spindash.counter = 0
		}
		else
		{
			Spindash.counter = Spindash.maxLimit
		}
	}

	return stringformat("spindash_0x%x", Spindash.counter)
}